import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    dependencies {
        classpath 'com.google.cloud.tools:jib-spring-boot-extension-gradle:0.1.0'
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'com.diffplug.spotless'
    id 'com.google.cloud.tools.jib'
    id 'io.github.vpavic.traintracker.conventions'
    id 'org.springframework.boot'
}

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/libs-milestone/' }
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation "de.vandermeer:asciitable:$libraryVersions.asciiTable"
    implementation 'org.apache.httpcomponents:httpclient'
    implementation "org.jsoup:jsoup:$libraryVersions.jsoup"
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.data:spring-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'
    implementation 'redis.clients:jedis'

    developmentOnly platform(SpringBootPlugin.BOM_COORDINATES)
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation platform("org.testcontainers:testcontainers-bom:$libraryVersions.testcontainers")
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:testcontainers'
}

checkstyle {
    toolVersion = toolVersions.checkstyle
}

spotless {
    java {
        licenseHeaderFile rootProject.file('config/spotless/license.java')
    }
}

jib {
    from {
        image = dockerImageVersions.base
    }
    pluginExtensions {
        pluginExtension {
            implementation = 'com.google.cloud.tools.jib.gradle.extension.springboot.JibSpringBootExtension'
        }
    }
}
